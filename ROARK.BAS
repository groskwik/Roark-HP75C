010 DELAY 1 @ DISP "******* ROARK'S FORMULA ********" @ DELAY 5
030 INPUT "Case:","2e";C$
040 C1$=C$[1,1] @ C2$=UPRC$(C$[2,2])
050 INPUT "L:","2000";L
060 INPUT "a:","200";A
070 INPUT "E:","200000";E
080 INPUT "I:","6670000"; I @ E1=E*I @ E2=24*E1
085 L1=.05*L @ L2=.95*L
090 IF C1$="1" THEN GOTO 110
100 IF C1$="2" THEN GOTO 120 ELSE 900
110 INPUT "W:","1000"; W @ GOTO 140
120 INPUT "wa:","0.1";W
130 INPUT "wl:","1.0";W1
140 INPUT "Step:","100";Q
150 DEF FNP(X) = (X>A)*(X-A)
160 DEF FNO(X)
170 IF X>A THEN FNO=1 ELSE FNO=0
180 END DEF
190 IF UPRC$(C$)="1A" THEN 205
191 IF UPRC$(C$)="1B" THEN 220
192 IF UPRC$(C$)="1C" THEN 240
193 IF UPRC$(C$)="1D" THEN 260
194 IF UPRC$(C$)="1E" THEN 280
195 IF UPRC$(C$)="1F" THEN 300
196 IF UPRC$(C$)="2A" THEN 330
197 IF UPRC$(C$)="2B" THEN 350
198 IF UPRC$(C$)="2C" THEN 370
199 IF UPRC$(C$)="2D" THEN 390
200 IF UPRC$(C$)="2E" THEN 410
201 IF UPRC$(C$)="2F" THEN 440
202 DISP "I don't know case ";C$
205 R1=0 @ M1=0 @ T1=W*(L-A)^2/(2*E1)
210 Y1=-W/(6*E1)*(2*L^3-3*L^2*A+A^3) @ GOTO 600
220 R1=0 @ M1=W*(L-A)^2/(2*L) @ T1=0
230 Y1=-W/(12*E1)*(L-A)^2*(L+2*A) @ GOTO 600
240 R1=W/(2*L^3)*(L-A)^2*(2*L+A) @ M1=0
250 T1=-W*A/(4*E1*L)*(L-A)^2 @ Y1=0 @ GOTO 600
260 R1=W/L^3*(L-A)^2*(L+2*A) @ M1=-W*A/L^2*(L-A)^2
270 T1=0 @ Y1=0 @ GOTO 600
280 R1=W/L*(L-A) @ M1=0 @ Y1=0
290 T1=-W*A/(6*E1*L)*(2*L-A)*(L-A) @ GOTO 600
300 R1=0 @ M1=W*(L-A) @ T1=0
310 Y1=-W*(L-A)/(6*E1)*(2*L^2+2*A*L-A^2) @ GOTO 600
330 R1=0 @ M1=0 @ T1=-W*(L-A)^3/(6*E1)+(W1-W)/(24*E1)*(L-A)^3
340 Y1=-W/(24*E1)*(L-A)^3*(3*L+A)-(W1-W)/(120*E1)*(L-A)^3*(4*L+A) @ GOTO 630
350 R1=0 @ M1=W*(L-A)^3/(6*L)+(W1-W)/(24*L)*(L-A)^3 @ T1=0
360 Y1=-W/(24*E1)*(L-A)^3*(L+A)-(W1-W)/(240*E1)*(L-A)^3*(3*L+2*A) @ GOTO 630
370 R1=W/(8*L^3)*(L-A)^3*(3*L+A)+(W1-W)/(40*L^3)*(L-A)^3*(4*L+A) @ M1=0
380 T1=-W/(48*E1*L)*(L-A)^3*(L+3*A)-(W1-W)/(240*E1*L)*(L-A)^3*(2*L+3*A) @ Y1=0 @ GOTO 630
390 R1=W/(2*L^3)*(L-A)^3*(L+A)+(W1-W)/(20*L^3)*(L-A)^3*(3*L+2*A) @ T1=0 @ Y1=0
400 M1=-W/(12*L^2)*(L-A)^3*(L+3*A)-(W1-W)/(60*L^2)*(L-A)^3*(2*L+3*A) @ GOTO 630
410 R1=W/(2*L)*(L-A)^2+(W1-W)/(6*L)*(L-A)^2 @ M1=0 @ Y1=0
420 T1=-W/(24*E1*L)*(L-A)^2*(L^2+2*A*L-A^2)-(W1-W)/(360*E1*L)*(L-A)^2*(7*L^2+6*A*L-3*A^2)
430 GOTO 630
440 R1=0 @ M1=W/2*(L-A)^2+(W1-W)/6*(L-A)^2 @ T1=0
450 Y1=-W*(L-A)^2/(24*E1)*(5*L^2+2*A*L-A^2)-(W1-W)/(120*E1)*(L-A)^2*(9*L^2+2*A*L-A^2)
460 GOTO 630
470 X1=FNROOT(0,L/2,FNT2(X)) @ Y1=FNY2(X1)
480 DEF FNV1(X) = R1-W*FNO(X)
490 DEF FNM1(X) = M1+R1*X-W*FNP(X)
500 DEF FNT1(X) = T1+M1*X/E1+R1*X^2/(2*E1)-W/(2*E1)*FNP(X)^2
510 DEF FNY1(X)
520 FNY1=Y1+T1*X+M1*X^2/(2*E1)+R1*X^3/(6*E1)-W/(6*E1)*FNP(X)^3
530 END DEF
540 DEF FNV2(X) = R1-W*FNP(X)-(W1-W)/(2*(L-A))*FNP(X)^2
550 DEF FNM2(X) = M1+R1*X-W/2*FNP(X)^2-(W1-W)/(6*(L-A))*FNP(X)^3
560 DEF FNT2(X) = T1+M1*X/E1+R1*X^2/(2*E1)-W/(6*E1)*FNP(X)^3-(W1-W)/(24*E1*(L-A))*FNP(X)^4
570 DEF FNY2(X)
580 FNY2=Y1+T1*X+M1*X^2/(2*E1)+R1*X^3/(6*E1)-W/E2*FNP(X)^4-(W1-W)/(120*E1*(L-A))*FNP(X)^5
590 END DEF
600 IF C2$="A" OR C2$="B" THEN X0=L ELSE GOTO 602
602 IF C2$="F" THEN X0=0 ELSE X0=FNROOT(L1,L2,FNV1(X))
605 M0=FNM1(X0)
610 IF C2$="A" OR C2$="B" THEN X1=0 ELSE X1=FNROOT(L1,L2,FNT1(X))
615 Y2=FNY1(X1)
620 R2=-FNV1(L) @ M2=FNM1(L) @ GOTO 660
630 IF C2$="A" OR C2$="B" THEN X0=L ELSE X0=FNROOT(L1,L2,FNV2(X))
635 M0=FNM2(X0)
640 IF C2$="A" OR C2$="B" THEN X1=0 ELSE X1=FNROOT(L1,L2,FNT2(X))
645 Y2=FNY2(X1)
650 R2=-FNV2(L) @ M2=FNM2(L)
655 DISP "***** REACTIONS AT SUPPORTS ****"
660 IF ABS(R1)>0 THEN DISP "RA:";R1
670 IF ABS(R2)>0 THEN DISP "RB:";R2
680 IF ABS(M1)>0 THEN DISP "MA:";M1
690 IF ABS(M2)>0 THEN DISP "MB:";M2
710 DISP "******** MAXIMAL VALUES ********"
720 DISP USING 905 ; X0,M0
730 DISP USING 907 ; X1,Y2
740 DIM T$(3),H$(3),Z(3)
750 T$(1)="********** DEFLECTION **********"
752 T$(2)="************ MOMENT ************"
755 T$(3)="********** SHEAR FORCE *********"
760 H$(1)="        X             Y(X)"
765 H$(2)="        X             M(X)"
767 H$(3)="        X             V(X)"
770 FOR I=1 TO 3
780 DISP T$(I)
790 DISP H$(I)
800 FOR X=0 TO L+Q STEP Q
810 IF X>L THEN X=L
820 IF C1$="1" THEN Z(1)=FNY1(X) ELSE Z(1)=FNY2(X)
830 IF C1$="1" THEN Z(2)=FNM1(X) ELSE Z(2)=FNM2(X)
840 IF C1$="1" THEN Z(3)=FNV1(X) ELSE Z(3)=FNV2(X)
850 DISP USING 910 ; X,Z(I)
860 IF X=L THEN X=L+Q
870 NEXT X
880 NEXT I
890 END
900 DISP "I don't know case ";C1$
905 IMAGE "X:",x,6d.3d,4x,"M:",x,d.3de
907 IMAGE "X:",x,6d.3d,4x,"Y:",x,d.3de
910 IMAGE 6d.2d,5x,d.5de
*EOF*

